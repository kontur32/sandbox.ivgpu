module namespace transform = 'transformator';

declare namespace a = 'urn:schemas-microsoft-com:xml-diffgram-v1';
declare namespace b = 'http://tempuri.org/dsMMISDB.xsd';

import module namespace аттрибутыДисциплин = 'аттрибутыДисциплин' at 'subjectAttributes.xqm';

declare variable $transform:forms := 
  ( [ '1','очная' ],[ '2','заочная' ],[ '3','очно-заочная' ] );

declare function transform:P-to-simplex( $Data as element( Документ )* ){
let $Планы := $Data

for $План in $Планы
let $Титул := $План/План/Титул
let $НазваниеНаправления := 
  normalize-space(
    substring-after( $Титул/Специальности/Специальность[ 1 ]/@Название/data(), ' ' )
  )
let $НазваниеПрофиля := 
  normalize-space(
    replace(
      substring-after( $Титул/Специальности/Специальность[ 2 ]/@Название/data(), ' ' ),
      'программа', ''
    )
  )
return
  <Программа 
    КодНаправления = '{ normalize-space( $Титул/@ПоследнийШифр ) }' 
    НазваниеНаправления = '{ replace( $НазваниеНаправления, "\d{2}.", "")   }' 
    НазваниеПрофиля = '{ normalize-space( $НазваниеПрофиля ) }' 
    Кафедра = '{ $Титул/@КодКафедры }' 
    Год = '{ $Титул/@ГодНачалаПодготовки }' 
    ФормаОбучения = '{ $Титул/parent::*/@ФормаОбучения }'
    ФГОС = '3P'>
    <Файл DETAIL__URL = '{ $План/@DETAIL__URL/data() }' ID = '{ $План/@ID/data() }'/>
    <Дисциплины>
      {
        for $i in $План/План/СтрокиПлана/Строка
        return
          <Дисциплина КодДисциплины = '{ $i/@ИдетификаторДисциплины }' Название = '{ $i/@Дис }' КодКафедры = '{ $i/@Кафедра }' ЗЕТ = '{ $i/@КредитовНаДисциплину }'>
            {
              for $i in $i/child::*
              return
                element{ 'Семестр'}{
                  $i/attribute::*
                }
            }
            <Компетенции>
              {
                for $i in $План/План/Компетенции/Строка[ @Код = tokenize( $i/@КомпетенцииКоды, '&amp;' ) ]
                return
                  <Компетенция ШифрКомпетенции = '{ $i/@Индекс }' Название = '{ $i/@Содержание }'/>
              }
            </Компетенции>
          </Дисциплина>
      }
    </Дисциплины>
  </Программа>
};

declare function transform:PP-to-simplex( $Data as element( Документ )* ){
let $Планы :=
  $Data/a:diffgram/b:dsMMISDB

for $План in $Планы
let $КодАктивногоПлана := $План/b:Планы/@КодАктивногоООП/data()
let $OOP := $План/b:ООП/b:ООП[ @Код = $КодАктивногоПлана  ]
return
  <Программа 
    КодНаправления = '{ $План/b:ООП/@Шифр }' 
    НазваниеНаправления = '{ $План/b:ООП/@Название  }' 
    НазваниеПрофиля = '{ $OOP/@Название}' 
    Кафедра = '{ $План/b:Планы/@КодПрофКафедры }' 
    Год = '{ $План/b:Планы/@ГодНачалаПодготовки }'
    ФормаОбучения = '{ $transform:forms[ .?1 =  $План/b:Планы/@КодФормыОбучения ]?2 }'
    КодФормыОбучения = '{ $План/b:Планы/@КодФормыОбучения }'
    ФГОС = '3PP'
    датаПриказа = '{ substring-before( $OOP/parent::*/@ДатаДокумента/data(), "T" ) }'
    номерПриказа = '{ $OOP/parent::*/@НомерДокумента/data() }'
    датаСовета = '{  substring-before( $План/b:Планы/@ДатаУтверСоветом/data(), "T" )  }'
    номерСовета = '{  $План/b:Планы/@НомПротокСовета/data() }'>
    <Файл DETAIL__URL = '{ $План/parent::*/parent::*/@DETAIL__URL }' ID = '{ $План/parent::*/parent::*/@ID}' CREATE__TIME = '{ $План/parent::*/parent::*/@CREATE__TIME }' UPDATE__TIME = '{ $План/parent::*/parent::*/@UPDATE__TIME }'/>
    <Дисциплины>
      {
        for $i in $План/b:ПланыСтроки[ @ТипОбъекта = ( '2', '3', '6' ) and ( ( @КодООП/data() = $План/b:ООП/@Код/data() ) or ( @КодООП/data() = $КодАктивногоПлана ) ) ]
        
        let $discComp := 
          $План/b:ПланыКомпетенцииДисциплины
            [ @КодСтроки/data() = $i/@Код/data() ]/@КодКомпетенции/data()
        let $формыКонтроляПоСеместрам := 
          аттрибутыДисциплин:формыКонтроляПоСеместрам3PP( $План, $i/@ДисциплинаКод )
        return
          <Дисциплина 
            КодДисциплины = '{ $i/@ДисциплинаКод }'
            Название = '{ $i/@Дисциплина }'
            КодКафедры = '{ $План/b:Кафедры[ @Код = $i/@КодКафедры ]/@Номер/data() }'
            ЗЕТ = '{$i/@ЗЕТфакт}'
            Семестр = '{ $формыКонтроляПоСеместрам[ 1 ] }'
            ФормыКонтроля = '{ $формыКонтроляПоСеместрам[ 2 ] }'
            Тип = '{ $i/@ТипОбъекта/data() }'
            >
            <видыРабот>{
              аттрибутыДисциплин:видыРаботПоДисциплине( $План, $i/@ДисциплинаКод)
            }</видыРабот>
            <Компетенции>
              {
                for $i in $План/b:ПланыКомпетенции [ @Код/data() = $discComp ]
                return
                  <Компетенция 
                    ШифрКомпетенции = '{ $i/@ШифрКомпетенции }' 
                    Название = '{ $i/@Наименование }'
                  />
              }
            </Компетенции>
          </Дисциплина>
      }
    </Дисциплины>
  </Программа>
};