module namespace аттрибутыДисциплин = 'аттрибутыДисциплин';

declare namespace a = 'urn:schemas-microsoft-com:xml-diffgram-v1';
declare namespace b = 'http://tempuri.org/dsMMISDB.xsd';

declare 
  %public
function 
  аттрибутыДисциплин:формыКонтроляПоСеместрам3PP(
    $план as element()*, $кодДисциплины as xs:string
  ) as item()*
{
    let $формыКонтроля := 
      $план/b:СправочникВидыРабот[ @КодТипРабот = '7' ] (: 7 - контроль :)
    
    let $дисциплина := 
      $план/b:ПланыСтроки[ @ДисциплинаКод = $кодДисциплины  ]/@Код/data()
    
    let $видыРаботВсе :=
      $план/b:ПланыНовыеЧасы
      [ @КодОбъекта = $дисциплина ]
      [ @КодВидаРаботы = $формыКонтроля/@Код/data() ]
    
    let $видПлана := 
      if( $видыРаботВсе[ @Семестр/data() = "0" ] )
      then( "1" ) (: виды работа по сессиям :) 
      else( "0" ) (: виды работ оп семестрам :)
   
    let $видыРаботПоДисциплине := 
      $видыРаботВсе
      [ if( $видПлана = "1" )then( @Семестр/data() = "0" )else( true() ) ]
    
    
    let $result := 
      for $i in $видыРаботПоДисциплине
      let $семестр := 
        if( $видПлана = "0" )
        then( ( $i/@Курс - 1 ) * 2 +  $i/@Семестр )
        else( ( $i/@Курс - 1 ) * 2 +  $i/@Сессия )
        
      let $формаКонтроля :=
        $семестр || ":" || $формыКонтроля[ @Код = $i/@КодВидаРаботы ]/@Название/data()
      
      order by $семестр
      return
       [ $семестр, $формаКонтроля ]
    return
      (
        string-join( distinct-values( $result?1 ), ','),
        string-join( $result?2, ',')
      )
};

declare
  %public
function аттрибутыДисциплин:видыРаботПоДисциплине( $план as element()*, $кодДисциплины as xs:string )
  as element( видРабот )*
{
  let $дисциплина := $план/b:ПланыСтроки[ @ДисциплинаКод = $кодДисциплины ]
  
  
  let $p :=
    $план/b:ПланыНовыеЧасы
    [ @КодОбъекта = $дисциплина/@Код/data() ]
  
  let $видПлана := 
      if( $p[ @Семестр/data() = "0" ] )
      then( "1" ) (: виды работа по сессиям :) 
      else( "0" ) (: виды работ оп семестрам :)
  
  let $результат :=
    for $i in  $p[ if( $видПлана = "1" )then( @Семестр/data() = "0" )else( true() ) ]
    
    let $семестр := 
      xs:string( $i/attribute::Курс * 2 - 2 + $i/attribute::Семестр )
    
    let $семестр := 
        if( $видПлана = "0" )
        then( ( $i/@Курс - 1 ) * 2 +  $i/@Семестр )
        else( ( $i/@Курс - 1 ) * 2 +  $i/@Сессия )
        
    let $видРабот :=
      $план/b:СправочникВидыРабот
      [ @Код/data() = $i/attribute::КодВидаРаботы/data() ]
      /@Аббревиатура/data()
    
    where $i/attribute::КодТипаЧасов/data() = "1"
    order by number( $i/attribute::КодВидаРаботы/data() )
    order by $семестр
    return
        <видРабот>
          <семестр>{ $семестр }</семестр>
          <кодВидаРабот>{ $i/attribute::КодВидаРаботы/data() }</кодВидаРабот>
          <названиеВидаРабота>{ $видРабот}</названиеВидаРабота>
          <часы>{ $i/attribute::Количество/data() }</часы>
        </видРабот>
   return
      $результат
};